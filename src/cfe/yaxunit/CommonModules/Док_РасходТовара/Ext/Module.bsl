
Процедура ИсполняемыеСценарии() Экспорт
    
   ЮТТесты.ДобавитьТестовыйНабор("НазваниеГруппыТестов").ВТранзакции()
   	.ДобавитьТест("ТестПроверкаРасходаТоваровПозитивный", "", "Позитив")
	.ДобавитьТест("ТестПроверкаРасходаТоваровНегативны", "", "Негатив");

КонецПроцедуры

Процедура ТестПроверкаРасходаТоваровПозитивный() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
    Товар_Хлеб 		= СоздатьТовар("Хлеб");
	Склад_Основной 	= СоздатьСклад("Основной");
	Поставщик		= СоздатьКонтрагента("Поставщик", Справочники.ВидыЦен.Закупочная);
	Покупатель		= СоздатьКонтрагента("Покупатель", Справочники.ВидыЦен.Закупочная);
	Организация 	= СоздатьОрганизацию("ООО Рога и копыта");
	ВалютаRUB 		= СоздатьВалюту("RUB");

	
	// 2.1. Создаем документ  "Приход товара" за 05.12.2025 10:00
	Документ_ПриходТовара = СоздатьПриходТовара('20251205100000', Организация, Склад_Основной, Поставщик, ВалютаRUB, Товар_Хлеб, 56, 10);  

	// Проверка проведения
	ЮТест.ОжидаетЧто(Документ_ПриходТовара.ПолучитьОбъект()) 
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
	  	.НеВыбрасываетИсключение();
		
	// Проверка движений	
	ДвиженияДокумента_ПриходТовара_ТоварныеЗапасы = ЮТЗапросы.ДвиженияДокумента(Документ_ПриходТовара, "ТоварныеЗапасы");
		
	ЮТест.ОжидаетЧто(ДвиженияДокумента_ПриходТовара_ТоварныеЗапасы)
		.Заполнено()
		.ИмеетДлину(Документ_ПриходТовара.Товары.Количество())
		.КаждыйЭлементСоответствуетПредикату(
			ЮТест.Предикат()
				.Реквизит("ВидДвижения").Равно(ВидДвиженияНакопления.Приход)
				.Реквизит("Склад").Равно(Склад_Основной)
				.Реквизит("Товар").Равно(Товар_Хлеб)
				.Реквизит("Количество").Равно(10));
				
	ДвиженияДокумента_ПриходТовара_Взаиморасчеты = ЮТЗапросы.ДвиженияДокумента(Документ_ПриходТовара, "Взаиморасчеты");
		
	ЮТест.ОжидаетЧто(ДвиженияДокумента_ПриходТовара_Взаиморасчеты)
		.Заполнено()
		.ИмеетДлину(Документ_ПриходТовара.Товары.Количество())
		.КаждыйЭлементСоответствуетПредикату(
			ЮТест.Предикат()
				.Реквизит("ВидДвижения").Равно(ВидДвиженияНакопления.Приход)
				.Реквизит("Контрагент").Равно(Поставщик)
				.Реквизит("Сумма").Равно(560)); 
				
				
	// 2.2. Создаем документ  "Расход товара" за 05.12.2025 10:00 в количестве 5 шт.
	Документ_РасходТовара = СоздатьРасходТовара('20251206100000', Организация, Склад_Основной, Покупатель, ВалютаRUB, Товар_Хлеб, 72, 5);  

	// Проверка проведения
	ЮТест.ОжидаетЧто(Документ_РасходТовара.ПолучитьОбъект()) 
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
	  	.НеВыбрасываетИсключение();
		
	// Проверка движений	
	ДвиженияДокумент_РасходТовара_ТоварныеЗапасы = ЮТЗапросы.ДвиженияДокумента(Документ_РасходТовара, "ТоварныеЗапасы");
		
	ЮТест.ОжидаетЧто(ДвиженияДокумент_РасходТовара_ТоварныеЗапасы)
		.Заполнено()
		.ИмеетДлину(1)
		.КаждыйЭлементСоответствуетПредикату(
			ЮТест.Предикат()
				.Реквизит("ВидДвижения").Равно(ВидДвиженияНакопления.Расход)
				.Реквизит("Склад").Равно(Склад_Основной)
				.Реквизит("Товар").Равно(Товар_Хлеб)
				.Реквизит("Количество").Равно(5)); 
				
	ДвиженияДокумента_РасходТовара_Продажи = ЮТЗапросы.ДвиженияДокумента(Документ_РасходТовара, "Продажи");
		
	ЮТест.ОжидаетЧто(ДвиженияДокумента_РасходТовара_Продажи)
		.Заполнено()
		.ИмеетДлину(1)
		.КаждыйЭлементСоответствуетПредикату(
			ЮТест.Предикат()
				.Реквизит("Покупатель").Равно(Покупатель)
				.Реквизит("Товар").Равно(Товар_Хлеб)
				.Реквизит("Количество").Равно(5)
				.Реквизит("Сумма").Равно(360));

				
	ДвиженияДокумента_РасходТовара_Взаиморасчеты = ЮТЗапросы.ДвиженияДокумента(Документ_РасходТовара, "Взаиморасчеты");
		
	ЮТест.ОжидаетЧто(ДвиженияДокумента_РасходТовара_Взаиморасчеты)
		.Заполнено()
		.ИмеетДлину(1)
		.КаждыйЭлементСоответствуетПредикату(
			ЮТест.Предикат()
				.Реквизит("ВидДвижения").Равно(ВидДвиженияНакопления.Расход)
				.Реквизит("Контрагент").Равно(Покупатель)
				.Реквизит("Сумма").Равно(360)); 
				
	
	
КонецПроцедуры 

Процедура ТестПроверкаРасходаТоваровНегативны() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
    Товар_Хлеб 		= СоздатьТовар("Хлеб");
	Склад_Основной 	= СоздатьСклад("Основной");
	Поставщик		= СоздатьКонтрагента("Поставщик", Справочники.ВидыЦен.Закупочная);
	Покупатель		= СоздатьКонтрагента("Покупатель", Справочники.ВидыЦен.Закупочная);
	Организация 	= СоздатьОрганизацию("ООО Рога и копыта");
	ВалютаRUB 		= СоздатьВалюту("RUB");

	
	// 2.1. Создаем документ  "Приход товара" за 05.12.2025 10:00
	Документ_ПриходТовара = СоздатьПриходТовара('20251205100000', Организация, Склад_Основной, Поставщик, ВалютаRUB, Товар_Хлеб, 56, 10);  

	// Проверка проведения
	ЮТест.ОжидаетЧто(Документ_ПриходТовара.ПолучитьОбъект()) 
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
	  	.НеВыбрасываетИсключение();
				
				
	// 2.2. Создаем документ  "Расход товара" за текущий день в количестве 15 шт.
	Документ_РасходТовара = СоздатьРасходТовара(ТекущаяДатаСеанса(), Организация, Склад_Основной, Покупатель, ВалютаRUB, Товар_Хлеб, 72, 15);  

	// Проверка проведения
	ЮТест.ОжидаетЧто(Документ_РасходТовара.ПолучитьОбъект()) 
		.Метод("Записать")
		.Параметр(РежимЗаписиДокумента.Проведение)
		.Параметр(РежимПроведенияДокумента.Оперативный)
	  	.ВыбрасываетИсключение("Не удалось провести");
	
КонецПроцедуры 




// НСИ
Функция СоздатьВалюту(Наименование)

	Конструктор = ЮТест.Данные().КонструкторОбъекта(Справочники.Валюты)
        .ФикцияОбязательныхПолей()
		.Установить("Наименование", Наименование);  
		
		
	Возврат Конструктор.Записать();	
	
КонецФункции

Функция СоздатьКонтрагента(Наименование, ВидЦен = Неопределено)

	Конструктор = ЮТест.Данные().КонструкторОбъекта(Справочники.Контрагенты)
        .ФикцияОбязательныхПолей()
		.Установить("ВидЦен", 		ВидЦен)
		.Установить("Наименование", Наименование);  
		
		
	Возврат Конструктор.Записать();	
	
КонецФункции

Функция СоздатьОрганизацию(Наименование)

	Конструктор = ЮТест.Данные().КонструкторОбъекта(Справочники.Организации)
        .ФикцияОбязательныхПолей()
		.Установить("Наименование", Наименование);  
		
		
	Возврат Конструктор.Записать();	
	
КонецФункции

Функция СоздатьСклад(Наименование)

	Конструктор = ЮТест.Данные().КонструкторОбъекта(Справочники.Склады)
        .ФикцияОбязательныхПолей()
		.Установить("Наименование", Наименование);  
		
		
	Возврат Конструктор.Записать();	
	
КонецФункции  

Функция СоздатьТовар(Наименование)

	Конструктор = ЮТест.Данные().КонструкторОбъекта(Справочники.Товары)
        .ФикцияОбязательныхПолей()
		.Установить("Вид", 			Перечисления.ВидыТоваров.Товар)
		.Установить("Наименование", Наименование);  
		
		
	Возврат Конструктор.Записать();	
	
КонецФункции 

// Документы

Функция СоздатьПриходТовара(Дата, Организация, Склад, Поставщик, Валюта, Товар, Цена, Количество)
	
	Конструктор = ЮТест.Данные().КонструкторОбъекта(Документы.ПриходТовара)
		.Установить("Дата",  		Дата)
		.Установить("Организация", 	Организация)
		.Установить("Склад", 		Склад)
		.Установить("Поставщик", 	Поставщик)
		.Установить("Валюта", 		Валюта);
 
	Конструктор
        .ТабличнаяЧасть("Товары")
            .ДобавитьСтроку()
            .Установить("Товар", 		Товар)
            .Установить("Цена", 		Цена)
            .Установить("Количество", 	Количество)
			.Установить("Сумма", 		Цена*Количество);
		
	Возврат Конструктор.Записать();	
	
КонецФункции 

Функция СоздатьРасходТовара(Дата, Организация, Склад, Покупатель, Валюта, Товар, Цена, Количество)
	
	Конструктор = ЮТест.Данные().КонструкторОбъекта(Документы.РасходТовара)
		.Установить("Дата",  		Дата)
		.Установить("Организация", 	Организация)
		.Установить("Склад", 		Склад)
		.Установить("Покупатель", 	Покупатель)
		.Установить("ВидЦен", 		Справочники.ВидыЦен.Закупочная)
		.Установить("Валюта", 		Валюта);
		
	Конструктор
        .ТабличнаяЧасть("Товары")
            .ДобавитьСтроку()
            .Установить("Товар", 		Товар)
            .Установить("Цена", 		Цена)
            .Установить("Количество", 	Количество)
			.Установить("Сумма", 		Цена*Количество);
		
	Возврат Конструктор.Записать();	
	
КонецФункции